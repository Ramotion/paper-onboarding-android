apply plugin: 'com.android.library'
apply plugin: 'signing'
apply plugin: 'com.bmuschko.nexus'
apply plugin: "jacoco"

group = 'com.ramotion.paperonboarding'
version = '1.1.3'

android {
    compileSdkVersion 29
    buildToolsVersion "28.0.3"
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 5
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix " Debug"
            debuggable true
            testCoverageEnabled = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.12'
}

modifyPom {
    //noinspection GroovyAssignabilityCheck
    project {
        name 'Paper Onboarding for Android'
        description 'PaperOnboarding is a material design slider http://ramotion.com'
        url 'https://github.com/Ramotion/paper-onboarding-android'
        inceptionYear '2016'

        scm {
            url 'https://github.com/Ramotion/paper-onboarding-android'
            connection 'scm:git@github.com:Ramotion/paper-onboarding-android.git'
            developerConnection 'scm:git@github.com:Ramotion/paper-onboarding-android.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'https://opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'oleg-vasiliev'
                name 'Oleg Vasiliev'
                email 'oleg-vasiliev@hotmail.com'
            }

            developer {
                id 'golovin47'
                name 'Ivan Golovin'
                email 'golovinivans@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = false
    tests = false
    javadoc = false
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

jacoco {
    toolVersion = "0.7.6.201602180812"
    reportsDir = file("$buildDir/reports/jacoco")

}

task jacocoReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports after running tests.'

    reports {
        xml.enabled = true
        html.enabled = true
    }

    classDirectories = fileTree(
            dir: 'build/intermediates/classes/debug',
            excludes: [
                    '**/R*.class',
                    '**/BuildConfig*'
            ]
    )

    sourceDirectories = files('src/main/java')
    executionData = files('build/jacoco/testDebugUnitTest.exec')

    doFirst {
        files('build/intermediates/classes/debug').getFiles().each { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}